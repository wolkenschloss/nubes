import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'wolkenschloss.conventions.java'
    id 'io.quarkus'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation enforcedPlatform("io.quarkus:quarkus-universe-bom:1.13.7.Final")

    testImplementation platform("org.junit:junit-bom:5.7.2")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'io.quarkus:quarkus-junit5'

    integrationTestImplementation platform("org.testcontainers:testcontainers-bom:1.15.3")
    integrationTestImplementation platform("org.junit:junit-bom:5.7.2")
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
    integrationTestImplementation 'io.quarkus:quarkus-junit5'

    // Logging: Wir benutzen JBoss Log Manager, weil Quarkus ihn benutzt.
    // Das heißt aber nicht, dass wir den auch gleich als Abhängigkeit
    // angeben müssen. Der steckt irgendwo in den transitiven Abhängigkeiten.
    //
    // Ein Log Manger = Heile Welt.
    // Mehr als ein Log Manager = Hölle
    // implementation 'org.jboss.slf4j:slf4j-jboss-logmanager'
    // implementation 'org.jboss.logmanager:jboss-logmanager-embedded:1.0.9'
}

tasks.register("integrationTest", Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()

    testLogging {
        // options for log level LIFECYCLE.
        // LIFECYCLE is gradle's default log level.
        // Starting gradle with options -q or -w only
        // the summarize is will be shown
        // events "PASSED", "SKIPPED", "FAILED"
        // showStandardStreams = false

        // `./gradlew build --info`, um Details anzuzeigen.
        // Testergebnisse und Protokoll der Anwendung
        info {
            events "PASSED", "SKIPPED", "FAILED"
            exceptionFormat TestExceptionFormat.FULL

            // Durch diese Einstellung zeigt Gradle das Console Log von Quarkus
            // und den Tests an.
            showStandardStreams = true
        }
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    systemProperty "build.output.directory", buildDir.absolutePath

    // java.util.logging.manager nicht setzen! Wenn der Manager wie in der
    // folgenden Zeile gesetzt wird, kommt es zu doppelten Log-Ausgabe.
    // Einmal formatiert und ein zweites mal unformatiert.
    // systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

    // Wenn du das löschst oder änderst, bist du selbst schuld!
    systemProperty 'org.jboss.logging.provider', 'slf4j'

    // JUL sollten wir nicht benutzen. Sicherheitshalber setzen
    // wir hier mal das Ausgabeformat, falls doch mal eine Log
    // Ausgabe durchkommt. Sollte diese Ausgabe zu sehen sein,
    // stimmt die Logging Konfiguration nicht.
    systemProperty 'java.util.logging.SimpleFormatter.format', '[JUNIT] %3$s %4$s: %5$s%6$s%n'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter assemble
}

integrationTest.dependsOn(assemble)
check.dependsOn(integrationTest)

test {
    useJUnitPlatform()

    testLogging {
        // options for log level LIFECYCLE.
        // LIFECYCLE is gradle's default log level.
        // Starting gradle with options -q or -w only
        // the summarize is will be shown
        // events "PASSED", "SKIPPED", "FAILED"
        // showStandardStreams = false
        exceptionFormat TestExceptionFormat.SHORT

        // `./gradlew build --info`, um Details anzuzeigen.
        // Testergebnisse und Protokoll der Anwendung
        info {
            events "PASSED", "SKIPPED", "FAILED"
            exceptionFormat TestExceptionFormat.FULL

            // Durch diese Einstellung zeigt Gradle das Console Log von Quarkus
            // und den Tests an.
            showStandardStreams = true
        }
    }

    reports {
        junitXml.required = true
        html.required = true
    }

    // java.util.logging.manager nicht setzen! Wenn der Manager wie in der
    // folgenden Zeile gesetzt wird, kommt es zu doppelten Log-Ausgabe.
    // Einmal formatiert und ein zweites mal unformatiert.
    // systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"

    // Wenn du das löschst oder änderst, bist du selbst schuld!
    systemProperty 'org.jboss.logging.provider', 'slf4j'

    // JUL sollten wir nicht benutzen. Sicherheitshalber setzen
    // wir hier mal das Ausgabeformat, falls doch mal eine Log
    // Ausgabe durchkommt. Sollte diese Ausgabe zu sehen sein,
    // stimmt die Logging Konfiguration nicht.
    systemProperty 'java.util.logging.SimpleFormatter.format', '[JUNIT] %3$s %4$s: %5$s%6$s%n'
}
