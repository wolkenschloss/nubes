import com.github.gradle.node.npm.task.NpxTask
import com.github.gradle.node.npm.task.NpmInstallTask

plugins {
    id 'wolkenschloss.conventions.java'
    id 'com.github.node-gradle.node'
}

var destination = "$buildDir/classes/java/main/META-INF/resources"

node {
    version = "14.17.1"
    download = true
    npmInstallCommand = "ci"
}

sourceSets {
    main {
        output.dir destination, builtBy: ["vue"]
    }
}

def vue = tasks.register("vue", NpxTask) {

    group = "build"
    description = "build vue application"

    dependsOn tasks.named(NpmInstallTask.NAME)

    command = "vue-cli-service"
    args = ["build", "--dest", destination]

    inputs.files("package.json", "package-lock.json", "vue.config.js")
        .withPropertyName("configFiles")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir("src")
        .withPropertyName("scripts")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.cacheIf { true }
    outputs.dir(destination)
        .withPropertyName("resources")
}

def unit = tasks.register("unit", NpxTask) {
    group = "verification"
    description = "run jest unit tests"

    dependsOn tasks.named(NpmInstallTask.NAME)

    command = "vue-cli-service"
    args = ["test:unit"]
    inputs.files("package.json", "package-lock.json", "vue.config.js", "jest.config.js")
            .withPropertyName("configFiles")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir("src")
            .withPropertyName("scripts")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir("tests/unit")
            .withPropertyName("tests")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.cacheIf { true }
    outputs.dir("$buildDir/reports/tests/unit")
            .withPropertyName("reports")
}

def e2e = tasks.register("e2e", NpxTask) {
    group = "verification"
    description = "run e2e tests"

    dependsOn tasks.named(NpmInstallTask.NAME)

    command = "vue-cli-service"
    args = ["test:e2e", "--headless"]
    inputs.files("package.json", "package-lock.json", "vue.config.js")
            .withPropertyName("configFiles")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir("src")
            .withPropertyName("scripts")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    inputs.dir("tests/e2e")
            .withPropertyName("tests")
            .withPathSensitivity(PathSensitivity.RELATIVE)

    outputs.cacheIf { true }
    outputs.dir("$buildDir/reports/tests/e2e")
        .withPropertyName("reports")

}

build.dependsOn vue
check.dependsOn unit, e2e
