import com.github.gradle.node.npm.task.NpxTask
import com.github.gradle.node.npm.task.NpmInstallTask

plugins {
    id 'mycloud.java-conventions'
    id 'com.github.node-gradle.node'
}

var destination = "$buildDir/classes/java/main/META-INF/resources"

node {
    version = "14.17.1"
    download = true
}

sourceSets {
    main {
        output.dir destination, builtBy: ["vue"]
    }
}

def vue = tasks.register("vue", NpxTask) {

    def npmInstall = tasks.named NpmInstallTask.NAME
    dependsOn npmInstall
    group = "build"
    description = "build vue application"
    command = "vue-cli-service"
    args = ["build", "--dest", destination]
    // , "babel.config.js"
    inputs.files "package.json", "package-lock.json", "vue.config.js"
    inputs.dir "src"
    outputs.dir destination
}

def unit = tasks.register("unit", NpxTask) {
    group = "verification"
    description = "run jest unit tests"
    command = "vue-cli-service"
    args = ["test:unit"]
    inputs.files "package.json", "package-lock.json", "vue.config.js", "jest.config.js"
    inputs.dir "src"
    inputs.dir "tests/unit"
}

tasks.named("build").configure({
    dependsOn vue
})

tasks.named("check").configure({
    dependsOn(unit)
})
