import java.io.ByteArrayOutputStream
import java.nio.charset.StandardCharsets

plugins {
    java
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

val gitDescribe =  {
    val stdout = ByteArrayOutputStream()
    rootProject.exec {
        workingDir(projectDir)
        commandLine("git", "describe", "--tags")
        standardOutput = stdout
    }

    stdout.toString(StandardCharsets.UTF_8).trim()
}

tasks {
    val projectProperties by registering(WriteProperties::class) {
        group = "other"
        description = "Write project properties in a file."
        outputFile = file("${buildDir}/project.properties")
        encoding = "UTF-8"
        comment = "generated by wolkenschloss.conventions.java.gradle.kts"

        properties(mapOf(
                "project.name" to project.name,
                "project.group" to project.group,
                "project.version" to gitDescribe // that's deferred
        ))
    }

    processResources {
        from(projectProperties)
    }
}
